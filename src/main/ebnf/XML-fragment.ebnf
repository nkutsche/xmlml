document-fragment ::= XMLDecl? content EOF
XMLDecl  ::= '<?xml' VersionInfo? EncodingDecl? SDDecl? S? '?>'
VersionInfo
         ::= S 'version' Eq ( "'" VersionNum "'" | '"' VersionNum '"' )
Eq       ::= S? '=' S?


SDDecl   ::= S 'standalone' Eq ( "'" ( 'yes' | 'no' ) "'" | '"' ( 'yes' | 'no' ) '"' )
element  ::= '<' Name Attribute* S? ( '/>' | '>' content ETag )
Attribute
         ::= S Name Eq AttValue
ETag     ::= '</' Name S? '>'
content  ::= CharData? ( ( element | Reference | CDSect | PI | Comment ) CharData? )*

Reference
         ::= EntityRef
           | CharRef

CharRef  ::= '&#' CharRefDec ';'
           | '&#x' CharRefHex ';'
EntityRef
         ::= '&' Name ';'


EncodingDecl
         ::= S 'encoding' Eq ( '"' EncName '"' | "'" EncName "'" )
           
AttValue ::= ( '"' AttValueDouble '"'  ) | ( "'" AttValueSingle "'" ) 

AttValueDouble ::= ( AttValueDoubleVal | Reference )*
AttValueSingle ::= ( AttValueSingleVal | Reference )*    

Comment  ::= '<!--' CommentContent '-->'
CDSect   ::= '<![CDATA[' CDSectContent ']'+ ']>'

PI       ::= PIStart PITarget PIContentEnd 
<?TOKENS?>

EOF      ::= $
Char     ::= #x0009
           | #x000A
           | #x000D
           | [#x0020-#xD7FF]
           | [#xE000-#xFFFD]
           | [#x10000-#x10FFFF]
S        ::= ( #x0020 | #x0009 | #x000D | #x000A )+
NameStartChar
         ::= ':'
           | [A-Z]
           | '_'
           | [a-z]
           | [#x00C0-#x00D6]
           | [#x00D8-#x00F6]
           | [#x00F8-#x02FF]
           | [#x0370-#x037D]
           | [#x037F-#x1FFF]
           | [#x200C-#x200D]
           | [#x2070-#x218F]
           | [#x2C00-#x2FEF]
           | [#x3001-#xD7FF]
           | [#xF900-#xFDCF]
           | [#xFDF0-#xFFFD]
NameChar ::= NameStartChar
           | '-'
           | '.'
           | [0-9]
           | #x00B7
           | [#x0300-#x036F]
           | [#x203F-#x2040]
Name     ::= NameStartChar NameChar*


AttValueDoubleVal ::= [^<&"]*
AttValueSingleVal ::= [^<&']*


CharData ::= ( [^<&#x005D] | ']' [^<&#x005D] | ']' ']'+ [^<&#x005D>] )+ ']'*
           | ']'+
CommentContent  ::= ( Char - '-' | '-' ( Char - '-' ) )*
PIStart ::= '<?'
PIContentEnd     ::= ( S ( [^?] | '?'+ [^?>] )* '?'* )? '?>'
PITarget ::= Name
CDSectContent   ::= ( [^#x005D] | ']' [^#x005D] | ']' ']'+ [^#x005D>] )*
VersionNum
         ::= '1.' [0-9]+


Reference
         ::= EntityRef
           | CharRef

CharRef  ::= '&#' CharRefDec ';'
           | '&#x' CharRefHex ';'
   
CharRefDec ::=  [0-9]+        
CharRefHex ::=  [0-9a-fA-F]+        
EntityRef
         ::= '&' Name ';'

EncName  ::= [A-Za-z] ( [A-Za-z0-9._] | '-' )*